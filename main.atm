require "atmos.env.pico"

val X    = require "atmos.x"
val pico = require "pico"
val art  = require "art"
val map  = require "map"
val Civ  = require "civ"

func between (min, v, max) {
    ifs {
        v < min => min
        v > max => max
        else    => v
    }
}

set W = 50
set H = 50

pico.zet.title("Master of Pixelization");
pico.zet.size.window(@{x=5*W,y=5*H}, @{x=W,y=H})
pico.zet.expert(true);

set MAP = @{}
loop l in H {
    set MAP[l] = @{}
    loop c in W {
        set MAP[l][c] = nil
    }
}

math.randomseed()

spawn {
    every :draw {
        pico.zet.anchor.draw(:left, :top)
        pico.output.draw.buffer(@{x=0,y=0}, map)
        pico.zet.anchor.draw(:center, :middle)

        loop l in H {
            loop c in W {
                val clr = MAP[l][c]
                if clr {
                    pico.zet.color.draw(pico.color[clr])
                    pico.output.draw.pixel(c, l)
                }
            }
        }
    }
}

val ps = @{
    green = pico.pos(20,20),
    white = pico.pos(80,20),
    blue  = pico.pos(50,50),
    black = pico.pos(20,80),
    red   = pico.pos(80,80),
}

pin civs = tasks(5)
loop clr, pos in ps {
    spawn [civs] Civ(clr, pos)
}

every @1 {
    emit [:global] :turn
}
